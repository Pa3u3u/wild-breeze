package Leaf::Backlight;

use utf8;
use strict;
use warnings;

use parent      "Stalk::Driver";
use feature     qw(signatures);
no  warnings    qw(experimental::signatures);

use Breeze::Grad;

sub new($class, %args) {
    my $self = $class->SUPER::new(%args);

    $self->log->fatal("missing 'video' parameter in constructor")
        unless defined $args{video};

    $self->{video} = $args{video};
    return $self;
}

sub invoke($self) {
    my $max = qx(cat /sys/class/backlight/$self->{video}/max_brightness);
    my $cur = qx(cat /sys/class/backlight/$self->{video}/brightness);

    chomp($max, $cur);

    my $p = int ((100 * $cur) / $max);
    my $c = $self->theme->grad($p, '%{backlight.@grad,gray white}');

    my $ret = {
        icon      => "ï†…",
        text      => sprintf("%3d%%", $p),
        color     => $c,
    };

    if (($self->{last} // $p) != $p) {
        $ret->{invert} = 1;
    }

    $self->{last} = $p;
    return $ret;
}

sub on_wheel_up($) {
    system(qw(xbacklight -inc 5% -time 50 -steps 5));
    return { reset_all => 1, invert => 1 };
}

sub on_wheel_down($) {
    system(qw(xbacklight -dec 5% -time 50 -steps 5));
    return { reset_all => 1, invert => 1 };
}

sub on_middle_click($) {
    if ($_[0]->{last} < 10) {
        system(qw(xbacklight -set 40% -time 400 -steps 30));
    } else {
        system(qw(xbacklight -set  0% -time 400 -steps 30));
    }

    return { reset_all => 1, blink => 4 };
}

# vim: syntax=perl5-24

1;
